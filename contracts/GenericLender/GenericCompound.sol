pragma solidity ^0.6.12;
pragma experimental ABIEncoderV2;

import "../Interfaces/Compound/CErc20I.sol";
import "../Interfaces/Compound/InterestRateModel.sol";
import "@openzeppelinV3/contracts/token/ERC20/IERC20.sol";
import "@openzeppelinV3/contracts/math/SafeMath.sol";
import "@openzeppelinV3/contracts/utils/Address.sol";
import "@openzeppelinV3/contracts/token/ERC20/SafeERC20.sol";

import "../Interfaces/UniswapInterfaces/IUniswapV2Router02.sol";


import "./IGenericLender.sol";

/********************
 *   A lender plugin for LenderYieldOptimiser for any erc20 asset on compound (not eth)
 *   Made by SamPriestley.com
 *   https://github.com/Grandthrax/yearnv2/blob/master/contracts/GenericDyDx/GenericCompound.sol
 *
 ********************* */

contract GenericCompound is IGenericLender{

    using SafeERC20 for IERC20;
    using Address for address;
    using SafeMath for uint256;

    uint256 private constant blocksPerYear = 2_300_000;
    address public constant uniswapRouter = address(0x7a250d5630B4cF539739dF2C5dAcb4c659F2488D);
    address public constant comp = address(0xc00e94Cb662C3520282E6f5717214004A7f26888);
    address public constant weth = address(0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2);

    uint256 public minCompToSell = 0.5 ether;

    CErc20I public cToken;
    constructor(address _strategy,string memory name, address _cToken) public IGenericLender(_strategy, name) {
        cToken = CErc20I(_cToken);

        require(cToken.underlying() == address(want), "WRONG CTOKEN");

        want.approve(_cToken, uint256(-1));

    }

    function nav() external override view returns (uint256){
        return _nav();

    }

    function _nav() internal view returns (uint256){
        return want.balanceOf(address(this)).add(underlyingBalanceStored());

    }

    function underlyingBalanceStored() public view returns (uint256 balance){
        uint256 currentCr = cToken.balanceOf(address(this));
        if(currentCr == 0){
            balance = 0;
        }else{
            balance = currentCr.mul(cToken.exchangeRateStored()).div(1e18);
        }
    }

    function apr() external override view  returns (uint256){
        return _apr();
    }
    function _apr() internal view returns (uint256){
        return cToken.supplyRatePerBlock().mul(blocksPerYear);
    }
  
    function weightedApr() external override view  returns (uint256){
        uint256 a = _apr();
        return a.mul(_nav());
    }

    function withdraw(uint256 amount) external override management returns (uint256){
        return _withdraw(amount);
    }

    //emergency withdraw. sends balance plus amount to governance
    function emergencyWithdraw(uint256 amount) external override management{
        cToken.redeemUnderlying(amount);
        
        want.safeTransfer(vault.governance(),want.balanceOf(address(this)));

    }

    //withdraw an amount including any want balance
    function _withdraw(uint256 amount) internal  returns (uint256){

        uint balanceUnderlying = cToken.balanceOfUnderlying(address(this));
        uint looseBalance = want.balanceOf(address(this));
        uint total = balanceUnderlying.add(looseBalance);

        if(amount > total) {
            //cant withdraw more than we own
            amount = total;
        }
        if(looseBalance >= amount){
            want.safeTransfer(address(strategy),amount);
            return amount;
        }

        //not state changing but OK because of previous call
        uint liquidity = want.balanceOf(address(cToken));

        if(liquidity > 1) {
            uint256 toWithdraw = amount.sub(looseBalance);

            if(toWithdraw <= liquidity) {

                //we can take all
                cToken.redeemUnderlying(toWithdraw);
            } else {
                //take all we can
                cToken.redeemUnderlying(liquidity);
            }
        }
        _disposeOfComp();
        looseBalance = want.balanceOf(address(this));
        want.safeTransfer(address(strategy),looseBalance);
        return looseBalance;

    }

    function _disposeOfComp() internal {
        uint256 _comp = IERC20(comp).balanceOf(address(this));

        if (_comp > minCompToSell) {
            address[] memory path = new address[](3);
            path[0] = comp;
            path[1] = weth;
            path[2] = address(want);

            IUniswapV2Router02(uniswapRouter).swapExactTokensForTokens(_comp, uint256(0), path, address(this), now);
        }
    }
    function deposit() external override management{
        uint256 balance = want.balanceOf(address(this));
        cToken.mint(balance);

    }
    function withdrawAll() external override management returns (bool){
        uint256 invested = _nav();
        uint256 returned = _withdraw(invested);
        return returned >= invested;

    }

    //think about this
    function enabled() external override view returns (bool){
        return true;

    }
    function hasAssets() external override view returns (bool){
        return cToken.balanceOf(address(this)) > 0;

    }

    function aprAfterDeposit(uint256 amount) external override view returns (uint256){
        uint256 cashPrior = want.balanceOf(address(cToken));

        
        uint256 borrows = cToken.totalBorrows();

        
        uint256 reserves = cToken.totalReserves();

        uint256 reserverFactor = cToken.reserveFactorMantissa();

        InterestRateModel model = cToken.interestRateModel();

        //the supply rate is derived from the borrow rate, reserve factor and the amount of total borrows.
        uint256 supplyRate = model.getSupplyRate(cashPrior.add(amount), borrows,reserves, reserverFactor );

        return supplyRate.mul(blocksPerYear);

    }

    function protectedTokens() internal override view returns (address[] memory) {
        address[] memory protected = new address[](2);
        protected[0] = address(want);
        protected[1] = address(cToken);
        return protected;
    }

}